buildscript {
    repositories {
        maven {
            url 'https://labs.mastercontrol.com/artifactory/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
        
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.0.3"
    }
}

allprojects {
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
			if( release == "true") {
				repoKey = "libs-release-local"
			} else {
				repoKey = "libs-snapshot-local"
			}
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
            
        }
        defaults {
            publications ('LuceeCore')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
            
        }
    }
}

defaultTasks 'build'
ant.importBuild 'build.xml'
def libversion = ant.properties['number'] //from ant build.xml
def snapshotText = "-SNAPSHOT"
if (release == "true") {
	snapshotText = ""
}
publishing {
    publications {
        LuceeCore(MavenPublication) {
            groupId 'lucee'
            artifactId 'lucee-core'

            version libversion + snapshotText

            artifact file('dist/' + libversion + snapshotText +'.jar')
        }
    }
}


build.dependsOn {
    renameFile
}

artifactoryPublish.dependsOn {
    build
}

task renameFile(dependsOn: 'core') {
	doLast {
		println('renaming dist/' + libversion + '.lco to dist/' + libversion + snapshotText +'.jar') 
		file('dist/' + libversion +'.lco').renameTo(file('dist/' + libversion + snapshotText + '.jar'))
	}
}