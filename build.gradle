import java.text.SimpleDateFormat

//apply plugin: 'idea' // allows us to make an intelliJ IDEA project
//apply plugin: "com.jfrog.artifactory"
//apply plugin: 'java'
//apply plugin: 'maven'
//apply plugin: 'maven-publish'
buildscript {
    repositories {
        maven {
            credentials {
                username = artifactory_user
                password = artifactory_api_key
            }
            url "${artifactory_context_url}/repo"
            metadataSources {
                mavenPom()
                artifact()
            }
        }
        dependencies {
            classpath 'org.gradle:gradle-tooling-api:4.3'
            //classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
        }
    }
}

plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.15.1'
}
buildDir = 'builddir'
group = 'lucee'

idea.project {
    languageLevel = '17' // some MC Dependencies compiled with java 11 or beyond.
    vcs = 'Git'
}
idea {

    module {
        //if for some reason you want to add an extra sourceDirs
        sourceDirs = [file('lucee-java/lucee-core/src'),
                      file('lucee-java/lucee-loader/src')] as HashSet

    }
}


repositories {
    mavenLocal()

    maven {
        credentials {
            username = artifactory_user
            password = artifactory_api_key
        }
        url "${artifactory_context_url}/repo"
        metadataSources {
            mavenPom()
            artifact()
        }
    }
}
configurations {
    compile.extendsFrom provided
}

dependencies {
    implementation 'com.mastercontrol:s3-no-op-resource:0.3.1'
    implementation 'software.amazon.awssdk:s3:2.20.56'
    implementation 'antlr:antlr:2.7.6'
    implementation 'org.apache.sanselan:sanselan:0.97-incubator'
    implementation 'org.apache.commons:commons-compress:1.3'
    implementation 'org.apache.commons:commons-email:1.5'
    implementation 'org.apache.httpcomponents:httpmime:4.5.2'
    implementation 'org.apache.httpcomponents:httpclient:4.5.2'
    implementation 'commons-httpclient:commons-httpclient:3.0.1'
    implementation 'commons-collections:commons-collections:3.2.1'
    implementation 'commons-discovery:commons-discovery:0.4'
    implementation 'commons-fileupload:commons-fileupload:1.2.1'
    implementation 'commons-io:commons-io:2.1'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'commons-net:commons-net:3.1'
    implementation 'oro:oro:2.0.8'
    implementation 'org.apache.axis:axis-ant:1.4'
    // https://github.com/nec1704/axis1.4-java-patched
    implementation 'org.apache.axis:axis:1.4-20190730'
    implementation 'xalan:xalan:2.7.0'
    implementation 'xerces:xercesImpl:2.9.0'
    implementation 'backport-util-concurrent:backport-util-concurrent:2.2'
    implementation 'dom4j:dom4j:1.6.1'
    implementation 'net.sf.ehcache:ehcache-core:2.5.2'
    implementation 'org.lucee.lib:ESAPI:2.0.1'
    implementation 'org.lucee.lib:fonts:1.0.0' // for cfdocument
    implementation 'org.lucee.lib:fusiondebug-api-server:1.0.20'
    implementation 'com.h2database:h2:1.3.172' // a database lucee supports out of the box
    implementation(group: 'org.luceehibernate', name: 'hibernate-core', version: '3.5.5.1-Final')
    implementation 'org.lucee:hsqldb:2.3.3' // a database lucee supports out of the box
    implementation 'org.lucee.lib:icepdf-core:3.1.0_3'
    implementation 'org.lucee.lib:javasysmon:0.3.3' // tool behind getCPUUsage()
    implementation 'org.lucee.lib:jcifs:1.3.17' //SMB:// protocol for file operations
    implementation 'org.jfree:jfreechart:1.0.19' //cfchart
    implementation 'org.apache.tika:tika-core:2.8.0'
    implementation 'org.lucee.lib:jpedal_gpl:3.51b12' //looks like it makes thumbnails from pdfs
    implementation(group: "org.safehaus.jug", name: "jug", version: "2.0.0", classifier: "lgpl") //used for UUID creation
    implementation 'org.ow2.asm:asm-all:4.0' //cfml compilation
    implementation 'org.swinglabs:pdf-renderer:1.0.5' //adds watermarks to pdf, etc
    implementation 'javax.xml.stream:stax-api:1.0-2'
    implementation 'com.jcraft:jsch:0.1.53'
    implementation 'cssparser:cssparser:0.9.4'
    implementation 'javax.activation:activation:1.1'
    implementation 'org.apache.flex.blazeds:flex-messaging-core:4.8.0'
    implementation 'org.apache.axis:axis-jaxrpc:1.4'
    //compile 'org.apache.lucene:lucene-core:2.4.1'
    implementation 'org.lucee.lib:javax-servlet:1.4.1-01'
    implementation 'javax.servlet:servlet-api:2.3'
    implementation 'javax.mail:mail:1.4'
    implementation 'org.lucee.lib:openamf:1.5.8'
    implementation 'wsdl4j:wsdl4j:1.6.3'
    implementation 'org.lucee.lib:sun-jai_core:1.2.1' //cfimage
    implementation 'org.lucee.lib:sun-jai_codec:1.2.1' //cfimage
    implementation 'com.drewnoakes:metadata-extractor:2.6.2'
    implementation 'org.apache.poi:poi-ooxml:3.13'
    implementation 'org.apache.lucene:lucene-snowball:2.4.1'
    implementation 'org.apache.lucene:lucene-spellchecker:2.4.1'
    implementation 'org.apache.lucene:lucene-highlighter:2.4.1'
    implementation 'org.apache.lucene:lucene-analyzers:2.4.1'
    implementation 'org.apache.lucene:lucene-core:2.4.1'
    implementation 'com.hynnet:jacob:1.18'
    implementation 'org.lucee.lib:ojdbc14:10.1.0.5.0'
    implementation 'org.lucee.lib:xdb:1.0.0'
    implementation 'org.lucee.lib:xmlparserv2:1.2.2'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    implementation 'org.textmining:tm-extractors:0.4'
    implementation group: 'io.confluent', name: 'confluent-log4j', version: '1.2.17-cp12'
    implementation 'org.lucee.lib:sun-jndi-ldap:1.2.4'
    implementation 'org.lucee.lib:sun-jndi-ldapbp:1.2.4'
    implementation 'org.lucee.lib:sun-jndi-ldapsec:1.2.4'
    implementation 'org.lucee.lib:sun-jndi-providerutil:1.2.4'
    implementation 'org.lucee.lib:sun-security-jaas:1.2.4'
    implementation 'org.lucee.lib:sun-mail:1.3.3_01'
    implementation 'org.lucee.lib:sun-xml-saaj:1.2.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.4.0'
}

test {
    useJUnitPlatform()

    minHeapSize = "2g"
    maxHeapSize = "4g"

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

sourceSets {
    test {
        java {
            srcDirs = ['tests/junit']
        }
    }

    main {
        java {
            srcDirs = ['lucee-java']
        }
    }
}

defaultTasks 'build'
ant.properties.ct = new Date().getTime()
ant.importBuild('build.xml'){
    String oldTargetName -> return 'ant_' + oldTargetName
}
def libversion = ant.properties['number'] //from ant build.xml
def snapshotText = "-SNAPSHOT"
if (release == "true") {
    snapshotText = ""
}
version = libversion + snapshotText
publishing {
    publications {
        LuceeCore(MavenPublication) {
            version libversion + snapshotText
            from components.java
            artifacts = ['dist/' + project.version + '.jar']
        }
    }
}


build.dependsOn {
    renameFile
}
def now = new SimpleDateFormat('yyyy/MM/dd HH:mm:ss z').format(new Date())

task stampInfoFile(type: Copy) {
    into "builddir"
    from "lucee-java/lucee-core/src/lucee/runtime"
    include "Info.ini"
    filter{
        line -> line.replace("2015/01/01 00:00:00 CET",now)
    }
}

jar.dependsOn(stampInfoFile)
compileTestJava.dependsOn(stampInfoFile)
ant__core.dependsOn(stampInfoFile)


//artifactoryPublish.dependsOn {
//    build
//}

task renameFile(dependsOn: ['ant_core']) {
    doLast {
        println('renaming dist/' + libversion + '.lco to dist/' + libversion + snapshotText + '.jar')
        file('dist/' + libversion + '.lco').renameTo(file('dist/' + libversion + snapshotText + '.jar'))
    }
}

/*
Use this code block for a place to point a local tomcat install to.
task copyToLib(type: Copy) {
    into "builddir/libs"
    from configurations.runtimeClasspath
}

build.dependsOn(copyToLib)
*/
artifactory {
    contextUrl = "${artifactory_context_url}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            if (release == "true") {
                repoKey = "libs-release-local"
            } else {
                repoKey = "libs-snapshot-local"
            }
            username = artifactory_user
            password = artifactory_api_key
        }
        defaults {
            publications('LuceeCore')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = artifactory_user
            password = artifactory_api_key
        }
    }
}
